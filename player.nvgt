/*
	NVGT Player
	Copyright (c) 2024 Patrick Wilson (BrailleScreen)
	
	This software is provided "as-is", without any express or implied warranty. In no event will the authors be held liable for any damages arising from the use of this software.
	
	Permission is granted to anyone to use this software for any purpose, including commercial applications, and to alter it and redistribute it freely, subject to the following restrictions:
		1.	 The origin of this software must not be misrepresented; you must not claim that you wrote the original software. If you use this software in a product, an acknowledgment in the product documentation would be appreciated but is not required.
		2. Altered source versions must be plainly marked as such, and must not be misrepresented as being the original software.
		3. This notice may not be removed or altered from any source distribution.

*/

/*
Modified by Brandon Cross (ironcross32 on GitHub) starting on 07/02/2024
*/

#include "speech.nvgt"
#include "file_contents.nvgt"
#include "form.nvgt"
#include "dget.nvgt"

sound player;
const float BASE_VOLUME = -20;
float previous_volume;
stream_storage stream_list;
form_controls main_form_controls;
save_data savedata;

void main() {
	show_window("NVGT Player");
	audio_form@ main_form = setup_main_form();
	savedata.read_file();
	
	while(true) loop(@main_form);
}

audio_form@ setup_main_form() {
	audio_form mf;
	mf.create_window();
	stream_list.set_form(@mf);
	int stream_list = mf.create_list("Streams", maximum_items = 0, multiselect = false, repeat_boundary_items = true);
	main_form_controls.add("lst_streams", stream_list);
	int new_url_button = mf.create_button("Open new URl");
	main_form_controls.add("btn_new_url", new_url_button);
	int load_url_button = mf.create_button("Open URL from list");
	main_form_controls.add("btn_load_url", load_url_button);
	int output_devices_list = mf.create_list("Select Sound Output Device", 0, false, false);
	main_form_controls.add("lst_output_devices", output_devices_list);
	int change_output_device_button = mf.create_button("Change Output Device");
	main_form_controls.add("btn_change_output", change_output_device_button);
	populate_output_devices(@mf);
	int keyboard_area = mf.create_keyboard_area("Direct Keyboard Input Area");
	main_form_controls.add("kbd_main", keyboard_area);
	int exit_button = mf.create_button("E&xit", primary = false, cancel=true);
	main_form_controls.add("btn_exit", exit_button);
	mf.focus_silently(main_form_controls.get("lst_streams"));
	return (@mf);
}

void populate_output_devices(audio_form@ form) {
	string[]@ devices = get_sound_output_devices();
	devices.remove_at(0); // kill the no sound option
	uint len = devices.length();
	int cidx = main_form_controls.get("lst_output_devices");
	for (uint i=0;i<len;i++) {
		form.add_list_item(cidx, devices[i]);
	}
}

void change_output_device(audio_form@ form) {
	int cidx = main_form_controls.get("lst_output_devices");
	int lidx = form.get_list_position(cidx) + 1; // Because of removal of no sound option in populate_output_devices()
	sound_output_device = lidx;
}

void new_url() {
	if (player.active) {
		if (player.playing || player.paused) {
			player.stop();
		}
		player.close();
	}
	const string url = input_box("URL", "Enter the direct URL of a stream you would like to load.", "https://nwm.streamguys1.com/ktis-fm");
	if (!player.load_url(url)) {
		speak("There was an error loading the specified URL.");
		return;
	}
	const string name = input_box("", "Enter the name you'd like to assign this URL in the list.");
	if (name.is_empty()) {
		alert("Error", "Name cannot be empty", false, MESSAGE_BOX_ERROR);
		player.close();
		return;
	}
	// Add the stream name and URl to the stream_list object
	stream_list.add(name, url); // Will not add duplicates based on name
	// Set to a hopefully sane volume.
	player.volume = BASE_VOLUME;
	previous_volume = BASE_VOLUME;
	player.play();
}

void load_url_from_list(audio_form@ form) {
	int cidx = main_form_controls.get("lst_streams");
	int lidx = form.get_list_position(cidx);
	string itm = form.get_list_item(cidx, lidx);
	string url = stream_list.get(itm);
	if (player.active) {
		if (player.playing || player.paused) {
			player.stop();
		}
		player.close();
	}
	if (player.load_url(url)) {
		player.volume = BASE_VOLUME;
		previous_volume = BASE_VOLUME;
		player.play();
	} else {
		speak("There was an error loading the specified URL.");
	}
}

void loop(audio_form@ main_form) {
	wait(5);
	main_form.monitor();
	handle_form_input(@main_form);
	handle_keyboard_input();
}
void handle_form_input (audio_form@ main_form) {
	if (main_form.is_pressed(main_form_controls.get("btn_exit"))) on_exit();
	if (main_form.is_pressed(main_form_controls.get("btn_new_url"))) new_url();
	if (main_form.is_pressed(main_form_controls.get("btn_load_url"))) load_url_from_list(@main_form);
	if (main_form.is_pressed(main_form_controls.get("btn_change_output"))) change_output_device(@main_form);
}

void handle_keyboard_input() {
	if (key_pressed(KEY_SPACE)) handle_playback();
	if (key_down(KEY_LSHIFT) || key_down(KEY_RSHIFT) && key_repeating(KEY_UP)) change_volume(15);
	if (key_down(KEY_LSHIFT) || key_down(KEY_RSHIFT) && key_repeating(KEY_DOWN)) change_volume(-15);
	if (key_repeating(KEY_UP)) change_volume(5);
	if(key_repeating(KEY_DOWN)) change_volume(-5);
	if(key_pressed(KEY_V)) {
		string msg;
		if (player.volume <= -100) msg = "minimum";
		else if (player.volume >= 0) msg = "maximum";
		else msg = string(round(player.volume, 2));
		speak(msg);
	}
}

void handle_playback() {
	if (!player.active) return;
	/*
	It has come to my attention that pausing streams is dangerous because
	eventually, the connection will drop and never be restarted.
	hence, this function now simply mutes and unmutes the stream.
	*/
	
	if (player.volume <= -100 && previous_volume >= -100) {
		speak("Unmute");
		player.slide_volume(previous_volume, 300);
	} else {
		speak("Mute");
		player.slide_volume(-100, 300);
	}
}

void change_volume(float amount) {
	if (player.volume + amount > 0) {
		amount = 0 - player.volume;
		speak("Maximum");
	} else if (player.volume + amount < -100) {
		amount = -100 + player.volume;
		speak("Minimum");
	}
	previous_volume = player.volume;
	player.slide_volume((player.volume + amount), 300);
}

void on_exit() {
	savedata.write_file();
	exit();
}

class form_controls {
	dictionary controls;
	
	void add(const string&in name, const int &in cidx) {
		if (controls.exists(name)) {
			return;
		}
		controls.set(name, cidx);
	}
	
	void remove(const string&in name) {
		if (controls.exists(name)) {
			controls.delete(name);
		}
	}
	
	int get(const string&in name) {
		// return -2 on failure to avoid ambiguity with form errors
		return (dgetn(controls, name, def = -2));
	}
}

class stream_storage {
	dictionary streams;
	audio_form@ form;
	
	void set_form(audio_form@ f) {
		@form = f;
	}
	
	void add(const string&in name, const string&in url) {
		if (!streams.exists(name)) {
			streams.set(name, url);
			int cidx = main_form_controls.get("lst_streams");
			form.add_list_item(cidx, name, -1, false, true);
		}
	}
	
	void remove (const string&in name) {
		if (streams.exists(name)) {
			streams.delete(name);
		}
	}
	
	string get(const string&in url) {
		string itm = dgets(@streams, url, "");
		return itm;
	}
	
	string[]@ get_names() {
		string[]@ names = streams.get_keys();
		return @names;
	}
	
	json_object@ to_json() {
		json_object@ storage = parse_json("{}");
		if (streams.is_empty()) return (@storage);
		string[] keys = streams.get_keys();
		uint stream_size = streams.get_size();
		for (uint i=0;i < stream_size;i++) {
			wait(5);
			storage.set(keys[i], dgets(streams, keys[i]));
		}
		return @storage;
	}
	
	void to_dictionary(json_object@ loaded_contents) {
		uint stream_size = loaded_contents.size();
		if (stream_size == 0) return;
		string[] keys = loaded_contents.get_keys();
		string itm;
		for (uint i=0;i < stream_size;i++) {
			wait(5);
			itm = loaded_contents[keys[i]];
			streams.set(keys[i], itm);
		}
		populate_stream_list();
	}
	
	void populate_stream_list() {
		// Called automatically on loading from file
		if (streams.is_empty()) return;
		string[] keys = streams.get_keys();
		uint size = streams.get_size();
		int cidx = main_form_controls.get("lst_streams");
		for (uint i=0;i<size;i++) {
			form.add_list_item(cidx, keys[i], -1, false, true);
		}
	}
}

class save_data {
	string filename = "settings.json";
	void write_file() {
	json_object@ streams = stream_list.to_json();
	string file_contents = streams.stringify(2);
	if (file_contents.is_empty() || file_contents == "{}") return;
	put_file_contents(filename, file_contents);
	}
	
	void read_file() {
		json_object@ loaded_contents = parse_json("{}");
		string file_contents = get_file_contents(filename);
		if (file_contents.is_empty()) return;
		@loaded_contents = parse_json(file_contents);
		stream_list.to_dictionary(@loaded_contents);
	}
}